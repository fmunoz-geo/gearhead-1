Plot 0
	name <Squat the Squatters>
	% [[Ver1.5]]

	% **********************
	% *** ELEMENTS BLOCK ***
	% **********************

	% E1 is the faction behind the story
	Element1 <Grab 1>
	Element2 <Character Villainous !Comrade 1>
	Element3 <Grab 2>

	% ********************
	% *** SCRIPT BLOCK ***
	% ********************
	%  V1 = Time Limit
	%  V2 = Combat Indicator, if V2=1 combat has been entered
	%  V3 = Email sent indicator.
	%       If P3=-1, PC has been denied the job
	%  V4 = Original home of E2

	%  S101 = Faction Victory Count
	%  S102 = Enemy Victory Count (if appropriate)
	%  S103 = How many times PC fought for faction
	%  S104 = How many times PC fought against faction


	% On a cleanup request, delete this plot unless combat has been
	% entered.
	cleanup <if= V2 0 MoveChar E2 V4 AdvancePlot 0>

	% Time limit. This plot will conclude in one day, or immediately
	% if NPC E2 is killed.
	% At this startup, move the NPC to the factory site.
	start <ifNPCOK E2 else GoDelete if= V1 0 else GoCheckTime V= 1 ComTime V+ 1 86400 V= 4 EScene 2 MoveChar E2 N2>
	GoCheckTime <if= V2 0 ifG ComTime V1 MoveChar E2 V4 AdvancePlot 0>
	GoDelete <AdvancePlot 0>

	% Combat check. If combat has been entered, V2=1.
	% Combat ends in victory if the number of active masters on
	% team two drops to zero, or ends in defeat is the number of
	% active masters on team one drops to zero. After combat, V2=3
	% if the player was victorious or V2=2 is the player lost.
	% Finally, if the player won, the calling story's faction victory
	% count (by default now story variable 101) will be increased.
	nu1 <if= T1 0 if= V2 1  V= 2 2  LoseRenown>
	nu2 <if= T2 0 if= V2 1  V= 2 3  Reputation 6  1  XPV 50  S+ 101 1 FacXP+ E1 3>

	% Email check. If the PC belongs to the same faction as the NPC,
	% he may receive an email about the mission. Make a check every
	% 5 minutes.
	5Min <if= d30 23 if= V3 0 if= PCFac E1 ifG RReact E2 d50 ifG P1 0 V= 3 1 Email d5>


	% *********************
	% *** MESSAGE BLOCK ***
	% *********************

	% 01 - 05 :  Email Messages.
	%  The NPC is offering the PC a mission via email.
	Msg1 <\PERSONA E2 @ \SCENE EScene 2 :// The corporate will is being questioned. Report to \ELEMENT 3 to enforce our power.>
	Msg2 <\PERSONA E2 @ \SCENE EScene 2 :// \ELEMENT 1 has some dirty work that needs doing. Report here at once.>
	Msg3 <\PERSONA E2 @ \SCENE EScene 2 :// Those who oppose \ELEMENT 1 must be eliminated. Report here for duty.>
	Msg4 <\PERSONA E2 @ \SCENE EScene 2 :// I have a mission for you in \ELEMENT 3 ; you may get your hands dirty.>
	Msg5 <\PERSONA E2 @ \SCENE EScene 2 :// Residents of \ELEMENT 3 oppose our factory plan; they must be silenced.>


sub
	% This next bit is the conversation which will happen when the
	% PC speaks to E2.
	Persona 2
	rumor <!2 has been selected to work on the !3 factory project.>


	% ********************
	% *** SCRIPT BLOCK ***
	% ********************
	% If you're feeling faint of heart, just skip this
	% and jump straight to the messages block below.
	% Here are the variables used in this conversation:
	%  V1 = Job offer counter, V1=1 is job offered, V1=-1 if job denied
	%  V2 = Message Randomizer
	%  V3 = Threat value of enemy mecha
	%  V4 = Reward offered to PC for the mission
	% At startup, make sure the NPC has been moved to the proper scene.
	Greeting <NewChat if= P1 0 else GoRealStart Say 108>
	% Next, check to see whether the PC has already
	% completed the mission or not.
	GoRealStart <if= P2 3 else GoCheckLoss Say d5 CashPrize V4 AddReact d5 MoveChar E2 P4 AdvancePlot 0>
	GoCheckLoss <if= P2 2 else GoCheckJob V= 2 5 V+ 2 d5 Say V2 MoveChar E2 P4 AdvancePlot 0>
	GoCheckJob <if= V1 1 else GoCheckFirst Say 101 Goto GoAddComOps>

	% The first time the PC meets the NPC, the PC will be given the
	% mission automatically if he's part of the same faction, or if
	% he is a friend of the NPC, or if he's already been sent email.

	% The PC will be denied the mission if he's helped the enemy.
	GoCheckFirst <if= S104 0 else GoDenied if= V1 0 else GoDenied V= 1 -1 V= 2 75 V+ 2 d50 V= 3 Threat PCRep 6 V2 V= 4 Reward V3 200 Goto GoCheckFaction>

	GoCheckFaction <if= PCFac E1 else GoCheckEmail V= 2 8 V+ 2 d2 AddChat V2 Goto GoFirstDesc>
	GoCheckEmail <if= P3 1 else GoCheckFriend Goto GoFirstDesc>
	% If the reaction score is greater than 20, PC will automatically
	% be offered the job, but unlike above will not be automatically
	% expected to accept it,
	GoCheckFriend <ifG React 50 else GoNoAutomatic V= 2 15 V+ 2 d5 Say V2 V= 2 6 V+ 2 d2 AddChat V2 V= 2 8 V+ 2 d2 AddChat V2>
	GoNoAutomatic <ifG d20 PCRep 1 else GoDenied V= 2 20 V+ 2 d5 Say V2 V= 2 10 V+ 2 d3 AddChat V2 V= 2 13 V+ 2 d3 AddChat V2 V= 2 16 V+ 2 d3 AddChat V2>
	GoDenied <V= 1 -1 P= 3 -1 Say 106>

	GoFirstDesc <V= 1 1 V= 2 10 V+ 2 d5 Say V2 Goto GoAddComOps>

	GoAddComOps <V= 1 1 P= 3 1 AddChat d2 V= 2 2 V+ 2 d2 AddChat V2 V= 2 4 V+ 2 d2 AddChat V2 Memo 107>

	% The PC will enter combat here.
	result1 <EndChat V= 2 35 V+ 2 d5 Say V2 Goto GoStartCombat>
	GoStartCombat <Reputation 1 -1 SavePos S+ 103 1 P= 2 1 Dynamic 2 V3 .nu1 .nu2>
	.nu1 <if= T1 0 Return>
	.nu2 <if= T2 0 Salvage Return>
	result2 <Goto Result1>

	% The PC needs some time to get ready.
	result3 <NewChat Say 103>
	result4 <Goto Result3>

	% The PC will be told the enemy's point value.
	result5 <NewChat V= 2 25 V+ 2 d5 Say V2 AddChat 1 AddChat 3>
	result6 <Goto result5>

	% The PC accepts the job offer.
	result7 <NewChat Goto GoFirstDesc>
	result8 <Goto result7>

	% The PC refuses the job offer.
	result9 <NewChat Say 102 V= 1 -1 P= 3 -1 AddReact -d10 Reputation 1 1 FacXP+ E1 -5>
	result10 <Goto result9>

	% The PC tries to get the job via "PLAN A"- sheer villainy.
	result11 <NewChat ifG 0 PCRep 1 else GoR11Lose Goto GoFirstDesc>
	result12 <Goto result11>
	result13 <Goto result11>
	% PLAN "A", whatever it was, has failed...
	GoR11Lose <NewChat Say 104 V= 1 -1 P= 3 -1>


	% The PC tries to get the job via "PLAN B"- be uncaring.
	result14 <NewChat ifG d30 PCRep 3 else GoR14Lose Goto GoFirstDesc>
	result15 <Goto result14>
	result16 <Goto result14>
	GoR14Lose <NewChat Say 105 V= 1 -1 P= 3 -1>

	% The player doesn't want this stinkin' job.
	result17 <NewChat Say 106 Reputation 4 -d5 V= 1 -1 P= 3 -1>
	result18 <Goto result17>
	result19 <Goto result17>


	% *********************
	% *** MESSAGE BLOCK ***
	% *********************

	% 01 - 05 :  Player has won the battle and is being paid.
	Msg1 <Good work. Our factory project can proceed on schedule.>
	Msg2 <Here's your pay. We'd have to spend twice that much in lawyer fees had we decided to negotiate with them instead.>
	Msg3 <The mission was a complete success. Here's your money.>
	Msg4 <They're all gone. Here's your pay.>
	Msg5 <That went well. The factory project will now proceed as scheduled.>

	% 06 - 10 :  Player has lost the battle.
	Msg6 <You have failed me.>
	Msg7 <You allowed yourself to be beaten by that human trash. You have failed us.>
	Msg8 <The mission was a failure.>
	Msg9 <You failed.>
	Msg10 <You lost.>

	% 11 - 15 :  Description of mission.
	%  use "$ \VAL V4 " to insert the pay for the mission,
	%  and don't forget to mention that the PC also gets salvage.
	Msg11 <Some squatters are living on the grounds cleared for our factory. They must be removed. You'll earn $ \VAL V4 plus the salvage rights to their machines if you can defeat their mecha.>
	Msg12 <There's a community living on the grounds selected for our factory. They need to be removed. They have a small mecha force, but it should be easy to defeat them. Pay for this mission is $ \VAL V4 plus salvage rights.>
	Msg13 <There are squatters living on the ground reserved for our factory building. Of course, they can't remain there. The company will pay $ \VAL V4 plus salvage if you can destroy their community defense force.>
	Msg14 <There are people living on the grounds we purchased for the new factory. They are likely tree-huggers, ravagers, or other subversives. You will earn $ \VAL V4 plus salvage for forcing them out of the area.>
	Msg15 <It has been discovered that the ground we purchased for our new factory has people living upon it. Of course they have to go. Pay will be $ \VAL V4 plus first salvage rights if you can force them out.>

	% 16 - 20 :  Player is asked if he is looking for a job.
	Msg16 <I need you for an upcoming mission. There are people in \ELEMENt 3 who oppose \ELEMENT 1 and they must be dealt with harshly.>
	Msg17 <Are you looking for work? Our business venture in \ELEMENT 3 is threatened by squatters; they must be removed or destroyed.>
	Msg18 <\ELEMENT 1 could use your talents. There are protestors who oppose our \ELEMENT 3 factory; they must be eliminated.>
	Msg19 <I have a mission for you. Some people are opposed to the new \ELEMENT 1 factory in \ELEMENT 3 . They must be silenced.>
	Msg20 <I've got a job available for a mecha pilot. I need someone whose conscience will not interfere with the ability to carry out a difficult mission.>

	% 21 - 25 :  NPC implies job, PC might have chance to get it.
	Msg21 <\ELEMENT 1 has a job opening for a mecha pilot. You might qualify, as long as you're discrete and not shy about getting your hands dirty.>
	Msg22 <\ELEMENT 1 needs to hire a mecha pilot for some underhanded work. If you aren't the sort to have a conscience you could make good money here.>
	Msg23 <There is no room for morality in business. I need a mecha pilot to run a mission for the company... one who won't ask any difficult questions, so long as his pay is good.>
	Msg24 <There are people in \ELEMENT 3 who fail to acknowledge the supremacy of \ELEMENT 1 . They need to be chastised. Maybe fatally so.>
	Msg25 <In order to succeed a business must sometimes do difficult things. We cannot allow ourselves to be blinded by things such as conscience... I need a mecha pilot who shares this view.>

	% 26 - 30 :  PC will be told exact enemy point value.
	%  use "\VAL V3 " to insert the value.
	Msg26 <Our reports indicate that you'll be up against \VAL V3 threat points worth of enemy hardware. Do you feel up to the challenge?>
	Msg27 <The squatters have around \VAL V3 tactical points worth of equipment, which they normally use for bandit raids and other subversive activities.>
	Msg28 <The squatters have about \VAL V3 points worth of mecha, most of which comes from the ravagers who support them. Your job will be to disable all of that.>
	Msg29 <Their community has around \VAL V3 tactical threat points of mecha, most of which was undoubtedly provided by some rival corporation.>
	Msg30 <Our surveillance videos indicate that they have close to \VAL V3 points worth of equipment. It will be your job to demolish all of that.>

	% 36 - 40 :  Combat is starting.
	Msg36 <Good luck. Eliminate their defenses. Return here when you're done and we can discuss your pay.>
	Msg37 <Remember, their defenses must be completely destroyed. Return here after you've completed the mission.>
	Msg38 <These coordinates will take you to the inhabitated factory site. Make sure that no unauthorized mecha remain operational.>
	Msg39 <Proceed to these coordinates and engage any unauthorized mecha you encounter. Come back here when you're finished.>
	Msg40 <I'll upload the navigation data to your mecha. Return here once you've completed the mission and I'll give you your pay.>


	% 101 :  Player accepted job, then left to get ready.
	Msg101 <Are you ready to go yet?>
	% 102 :  Player has refused to take job; answer to prompts 9-10.
	Msg102 <There's no shortage of others who will do anything for money.>
	% 103 :  Player has asked for some time to get ready.
	Msg103 <Make sure that your equipment is in top condition, then return here and we can begin the operation.>
	% 104 :  "PLAN A" has failed, and the PC is not offered a job.
	Msg104 <I hardly think you're the kind to perform this sort of mission. I cannot afford to hire someone who does not posess the stomach to do what must be done.>
	% 105 :  "PLAN B" has failed, and the PC is not offered a job.
	Msg105 <Perhaps I have said too much...>
	% 106 :  Player doesn't want job; answer to prompts 17-19.
	msg106 <It's a good thing most citizens don't know what we have to do in order to ensure the prosperity of our nation.>
	% 107 :  Memo
	msg107 <\ELEMENT 2 in \SCENE EScene 2 offered you a combat mission.>
	% 108 :  NPC hasn't been moved yet.
	Msg108 <I'll be going to my new assignment in \ELEMENT 3 shortly.>

	% 01 - 02 :  Ready to start the mission
	Prompt1 <Let's begin the mission.>
	Prompt2 <I'm ready to go.>

	% 03 - 04 :  Need time to get ready first.
	Prompt3 <I need some time to get ready.>
	Prompt4 <Give me some time, first.>

	% 05 - 06 :  What is the enemy point value?
	Prompt5 <What will I be facing?>
	Prompt6 <How many mecha do they have?>

	% 07 - 08 :  After being asked if he wants a job, PC says yes.
	Prompt7 <I can do that.>
	Prompt8 <Give me the mission details.>

	% 09 - 10 :  After being asked if he wants a job, PC says no.
	Prompt9 <That's not my kind of work.>
	Prompt10 <I think you have the wrong person.>

	% 11 - 13 : Player tries to get job using PLAN "A".
	Prompt11 <What you need is a blackheart like me.>
	Prompt12 <You need dirty deeds, at reasonable rates?>
	Prompt13 <That sounds exactly like my kind of work.>

	% 14 - 16 : Player tries to get job using PLAN "B".
	Prompt14 <Eh, a job's a job. Give me the info.>
	Prompt15 <I can be very discrete.>
	Prompt16 <You shouldn't make these things known.>

	% 17 - 19 : Player doesn't much care for the job.
	Prompt17 <You disgust me.>
	Prompt18 <I'd never work for you.>
	Prompt19 <Forget it, that's not my style.>


end

