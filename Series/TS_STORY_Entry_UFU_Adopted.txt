Story
	name <STORY: Adopted>

	% V1 = Initialization Counter
	% S101 = Load new episode when S101 = 1
	% S102 = # PC Wins
	% S103 = Difficulcy Level, load ending when S103>75
	% S104 = # Wins when level last increased
	% S105 = Location Clue

	% The first START, force a conversation with Hyolee.
	Start <if= V1 0 else GoCheckClue  V= 1 1  MoreText .intro  S= 103 7  Print 1 ForceChat 1 History 2 History 3 Goto GoSetOps>
	GoSetOps <N= 2 0  NPCLevel N1 S103>
	.intro <TS_TEXT_Entry_UFU_Adopted.txt>
	Msg1 <Hyolee strikes up a conversation with you.>
	Msg2 <You were orphaned as a small child and never knew your parents.>
	Msg3 <You were raised in Hogye.>

	GoCheckClue <ifG S105 0 else GoNoClue Memo 5 S= 105 0>
	GoNoClue <ifG 0 S105 Memo 0 S= 105 0>
	Msg5 <Your personal quest has so far led to \NARRATIVE 3 .>

	Element1 <Prefab>
	Element2 <Faction GoodEnemy>
	Element3 <Scene>
	Stat 3 10

	XREnemy 0
	XRMystery 2
	XRBadThing 1

	hour <if= S101 1 else GoCheckHint ifG S103 75 else GoLoadEpisode XRanPlot .xend S= 101 0 S+ 103 d10>
	GoLoadEpisode <XRanPlot .xplot S= 101 0 ifG S102 S104 S+ 103 10 S+ 103 d10 S= 104 S102 NPCLevel N1 S103 ifFaction N2 PCFEnemy N2>
	GoCheckHint <if= d30 1 ifG 76 S103 StoryLine .xhint>
	.xplot <TS_XRAN_a###_*.txt>
	.xend <TS_XRAN_b###_*.txt>
	.xhint <TS_XRAN_Hint_*.txt>

inv
	NPC Mecha Pilot
	mecha <Vadel.txt>
	EquipChar 50000

	Plot 0
	% Note that I am hotloading this plot- DO NOT
	% DO THIS YOURSELF!!! I can get away with it because
	% I'm the programmer. Note that because this plot is
	% getting sneaked into the adventure riding under a
	% story gear, it will not be initialized by the
	% MATCH_PLOT_TO_ADVENTURE procedure. In this case
	% it shouldn't matter because the story is loaded at
	% the game's setup, and all the elements are starting
	% initialized. However, in general, I think this is
	% very bad form...
	name <PLOT: Temporary Personas>

	start <ifNPCOK E2 else GoDelete>
	GoDelete <AdvancePlot 0>

	% Element 1 is Hyolee.
	Element1 <Character>
	Stat 1 1

	% Element 2 is Megi.
	Element2 <Character>
	Stat 2 17
	sub
		Persona 1
		% Hyolee will greet the PC and tell him to go
		% speak with Megi.
		greeting <NewChat if= V1 0 else GoBeenBefore AddReact d30 V= 1 1 Say 1 AddChat 1 AddChat 2 AddChat 3>
		result1 <NewChat Reputation 4 d8 Say 2 AddChat 4 AddChat 5>
		result2 <NewChat Reputation 4 -d8 Say 3 AddChat 4 AddChat 5>
		result3 <NewChat Reputation 3 d10 Say 4 AddChat 4 AddChat 5>
		result4 <NewChat Say 103>
		result5 <NewChat Say 6>
		Msg1 <Hi \PC . I heard that you were planning to leave here soon. All ready for your trip?>
		Msg2 <Yeah, there isn't much here, unless you want to grow potatoes for the rest of your life... Oh, I heard that Megi wanted to see you before you leave. She probably has some advice or something. You know her...>
		Msg3 <Who isn't? Yeah, it's dull... but it's home. Remember, you can always come back here. Oh, that reminds me... Megi said that she wants to speak with you before you go. I guess she's going to give you some advice or something.>
		Msg4 <Sounds like fun, but I really can't. This is the best place for my research... no distractions. By the way, Megi said that she wants to see you before you leave. Maybe she wants to go with you herself? That's a scary thought...>
		Msg6 <In the same place you usually can, in her house, that white one over there with the red carpet... Hm, I hope that when you go exploring the world, you don't get lost. Your direction sense never was that good...>
		Prompt1 <I want to see the world.>
		Prompt2 <I'm bored with this town.>
		Prompt3 <Why don't you come with me?>
		Prompt4 <I'll go see her, then.>
		Prompt5 <Where can I find her?>
		GoBeenBefore <Say 101 AddChat 101 AddCHat 102>
		result101 <NewChat Say 102>
		result102 <NewChat Say 103>
		Msg101 <Hi. Have you spoken with Megi yet? She wanted to see you before you head out.>
		Msg102 <It's the white house with the red carpet. There's only one like it in town. Come to think of it, there aren't that many houses in Hogye, anyhow... You should be able to find it by trial and error if you had to.>
		Msg103 <Good luck, and come back to visit sometime!>
		Prompt101 <Where is Megi's house, again?>
		Prompt102 <I'm on my way.>



		Persona 2
		% Megi will speak with the PC and then allow
		% conversation branches to load the various job
		% plots, or the tutorial plot.
		greeting <NewChat if= V1 1 else GoFirstTime Say 1 AddChat 1 AddChat 2 AddChat 3 AddChat 4>

		result1 <NewChat Say 2 S= 101 1 StoryLine .Tutorial AdvancePlot 0>
		result2 <NewChat Say 3 S= 101 1 StoryLine .JobStory AdvancePlot 0>
		result3 <NewChat Say 4 S= 101 1 History 6 AdvancePlot 0>
		result4 <NewChat Say 5 S= 101 1 Reputation 2 -10 AddReact -30 StoryLine .CrimeStory AdvancePlot 0>

		.JobStory <ARC_TS_JobStory.txt>
		.CrimeStory <ARC_TS_CrimeStory.txt>
		.Tutorial <TS_PLOT_Tutorial.txt>

		Msg1 <So, \PC , is there anything I can do for you before you leave here?>
		Msg2 <You'll want to speak with Quentin in the general store about that.>
		Msg3 <I think that Doctor Kim needs some help over at the clinic... You can ask about a job there.>
		Msg4 <As you wish. I'll see you again when you return.>
		Msg5 <When you leave this town, please don't go ruining our good name. If anyone asks, tell them you're from Ipshil...>
		Msg6 <You did not request any help from Megi.>

		Prompt1 <Better give me the tutorial.>
		Prompt2 <I could use a first job.>
		Prompt3 <Thanks, but I don't need any help.>
		Prompt4 <Thought I'd rob a bank or something.>

		GoFirstTime <V= 1 1 Say 101 AddChat 101 AddChat 102>

		Result101 <NewChat Reputation 5 d8 Say 102 AddChat 103 AddChat 104 AddChat 105>
		Result102 <NewChat Reputation 7 -d8 Say 103 AddChat 103 AddChat 104 AddChat 105>
		result103 <NewChat Reputation 3 d8 Say 104 Goto GoAddJobOps>
		result104 <NewChat Reputation 5 -d8 Say 105 Goto GoAddJobOps>
		result105 <NewChat Reputation 3 -d8 Say 106 Goto GoAddJobOps>

		GoAddJobOps <AddCHat 1 AddCHat 2 AddCHat 3 AddChat 4>

		Msg101 <Hello \PC . I heard that you're planning to go soon. Are you planning to search for your history?>
		Msg102 <Many young people have been leaving town. I suppose there isn't much left here to keep them. Well, remember, you can always come back to your home anytime you like.>
		Msg103 <Good luck, but remember that the question of who you are is one that can only be answered from inside. Also, whatever you discover in your journeys, you can always call Hogye your home.>
		Msg104 <That's good to hear. I have one last thing to ask you... Do you require any help before you start your journey?>
		Msg105 <Is that how you truly feel? Well, I have one last thing to do for you before you go. If you require any help in preparation for your journey, all you have to do is ask.>
		Msg106 <I know, some things don't need to be said. Well, I have one more thing to say before you leave... Is there any way I can help you prepare for your journey?>

		Prompt101 <No, I just want to travel.>
		Prompt102 <Yes, I need to find out who I am.>
		Prompt103 <Thank you. It means a lot to me.>
		Prompt104 <Why? This town did nothing for me.>
		Prompt105 <You don't need to say; I know.>

	end

end
